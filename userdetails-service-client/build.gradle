plugins {
    id( 'java' )
    id( 'maven-publish' )
}

group 'au.org.ala'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.projectlombok', name:'lombok', version: '1.16.12'

    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.1.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-moshi', version: '2.1.0'
    compile group: 'com.squareup.moshi', name: 'moshi', version: '1.3.1'
    compile group: 'com.squareup.moshi', name: 'moshi-adapters', version: '1.3.1'

    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
    testCompile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.3.1'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task integrationTest(type: Test) {
    mustRunAfter test
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
