plugins {
    id( 'java' )
    id( 'maven-publish' )
}
apply from: 'https://raw.githubusercontent.com/AtlasOfLivingAustralia/travis-build-configuration/master/travis_gradle_java_publish.gradle'

group 'au.org.ala'
version '1.5.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compileOnly group: 'org.projectlombok', name:'lombok', version: '1.16.12'

    compile group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.4.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-moshi', version: '2.4.0'
    compile group: 'com.squareup.moshi', name: 'moshi', version: '1.5.0'
    compile group: 'com.squareup.moshi', name: 'moshi-adapters', version: '1.5.0'

    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
    testCompile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.10.0'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.10.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '2.6.0'
}

task integrationTest(type: Test) {
    mustRunAfter test
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}
